apply plugin: 'groovy'
apply plugin: 'maven'
apply plugin: 'release'

buildscript {
   repositories {
      mavenCentral()
   }
   dependencies {
      classpath 'com.github.townsfolk:gradle-release:1.2'
   }
}

sourceCompatibility = '1.6'

configurations {
    mavenDeploy
}

repositories {
    mavenCentral()
    maven { url 'http://releases.marmeladburk.fidesmo.com/' }
}

dependencies {
    compile gradleApi()
    compile localGroovy()
    compile 'com.fidesmo:gradle-javacard:0.2.6'
    compile 'com.fidesmo:sec-client-core:0.2.2'
    compile 'com.fidesmo:sec-client-delivery:0.2.2'
    compile 'com.fidesmo:sec-client-local:0.2.2'
    compile 'io.github.jnasmartcardio:jnasmartcardio:0.2.4'
    compile 'jline:jline:2.13'
    runtime 'ch.qos.logback:logback-classic:1.1.3'
    testCompile 'junit:junit:4.11'
    testCompile 'org.hamcrest:hamcrest-all:1.3'
    mavenDeploy 'org.kuali.maven.wagons:maven-s3-wagon:1.2.1'
}

group 'com.fidesmo'

uploadArchives {
    repositories {
        mavenDeployer {
            uniqueVersion = false
            configuration = configurations.mavenDeploy
            repository(url: "s3://${version.endsWith('-SNAPSHOT') ? 'snapshots' : 'releases'}.marmeladburk.fidesmo.com")
            pom.project {
                name 'Gradle fidesmo plugin'
                description 'Gradle plugin for easy development and deplyoment of java cardlets with the fidesmo service'
                url 'https://github.com/fidesmo/gradle-plugin'
                inceptionYear '2014'

                artifactId 'gradle-fidesmo'

                scm {
                    url 'https://github.com/fidesmo/gradle-fidesmo'
                    connection 'scm:https://github.com/fidesmo/gradle-fidesmo'
                    developerConnection 'scm:git@github.com:fidesmo/gradle-fidesmo.git'
                }

                licenses {
                    license {
                        name 'The Apache Software License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                        distribution 'repo'
                    }
                }

                developers {
                    developer {
                        id 'yves'
                        name 'Yves MÃ¼ller'
                        email 'yves.mueller@fidesmo.com'
                        organization 'Fidesmo AB'
                        organizationUrl 'http://fidesmo.com'
                    }
                }
            }
        }
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = '2.0'
}
